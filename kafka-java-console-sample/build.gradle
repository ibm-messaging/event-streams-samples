/**
 * Copyright 2015-2018 IBM
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
*/
//
// Licensed Materials - Property of IBM
// Â© Copyright IBM Corp. 2015-2018
//
plugins {
  id 'application'
  id 'java'
  id 'com.github.johnrengelman.shadow' version '4.0.2'
  id 'eclipse'
}

group = 'com.ibm.eventstreams.samples'
version = '2.0'
mainClassName = 'com.eventstreams.samples.EventStreamsConsoleSample'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.apache.kafka:kafka-clients:2.4.+'
  compile 'log4j:log4j:1.2.17'
  compile 'org.slf4j:slf4j-log4j12:1.7.25'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.9.7'
}

task downloadDependencies(type: Exec) {
  configurations.testRuntime.files
  commandLine 'echo', 'Downloaded all dependencies'
}

// Compile source code
compileJava {
    doFirst {
        copy {
            from configurations.runtime
            into 'lib'
        }
    }

    source = 'src'
    options.encoding = 'ISO-8859-1'
}

jar {
    from "resources/log4j.properties"
    
    doLast {
        copy {
            from 'resources'
            into 'build/libs/resources'
        }
    
        copy {
            from 'lib'
            into 'build/libs/lib'
        }
    }
    
    // Provide Main-Class so the JAR executes properly, along with the required classpath data.
    manifest {
        attributes('Main-Class': mainClassName,
        'Class-Path': configurations.runtime.files.collect { './lib/' + it.name }.join(' '))
    }
}

// Save JAR in build directory.
uploadArchives {
    repositories {
        flatDir {
            dirs 'build'
        }
    }
}

['Zip', 'Tar'].each { suffix ->
    "dist$suffix" {
        def basePath = baseName + '-' + version

        from('resources') {
            into { basePath + '/resources' }
            exclude 'log4j.properties'
        }

        from('resources') {
            into { basePath + '/lib' }
            include 'log4j.properties'
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "src/main/resources"]
        }
    }
}